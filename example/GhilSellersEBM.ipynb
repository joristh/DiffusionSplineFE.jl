{
 "cells": [
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "d1880d84",
   "metadata": {},
   "outputs": [],
   "source": [
    "using CairoMakie, DiffusionSplineFE, OrdinaryDiffEq, Interpolations"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "dca46b0f",
   "metadata": {},
   "outputs": [],
   "source": [
    "function load_interpolated_parameters() \n",
    "     # Input Parameters from Sellers Paper, estimated from observational data for one hemisphere\n",
    "     colat_1 = (-90.:10.:90.)./90. # used for C,Q,T_0\n",
    "     colat_2 = (-85.:10.:85.)./90. # used for b,z,k_1,k_2\n",
    "     \n",
    "     # An intial condition given in the paper\n",
    "     T_0 = [247.3625, 252.0740, 262.5715, 271.2980, 278.9325, 285.7530, 291.4090, 296.0815, 298.7815, 299.3510];\n",
    "     \n",
    "     # Combined air-land-sea effective heat capacity [cal/cm^2/K] / on grid\n",
    "     C = [500, 1000, 1500, 4725, 5625, 5812, 5813, 5625, 6000, 5625] \n",
    " \n",
    "     # High-frequency solar irradiance [cal/cm^2/sec]\n",
    "     Q = [0.426, 0.440, 0.484, 0.579, 0.696, 0.804, 0.894, 0.961, 1.003, 1.017]*1e-2;\n",
    " \n",
    "     # first value in the Ghil paper may be a typo, correction suggested by Tomas Bodai \n",
    "     b = [2.912, 2.96, 2.934, 2.914, 2.915, 2.868, 2.821, 2.804, 2.805]\n",
    " \n",
    "     z = [1204.5, 820.0, 295.0, 150.5, 193.5, 301.0, 261.0, 133.5, 156.0] #[m]\n",
    " \n",
    "     # Eddy diffusivity coefficients; k1*T'x: sensible heat flux,\n",
    "     # k2*g(T)*T'x: latent heat flux\n",
    "     k_1 = [0.47113, 0.61988, 1.19933, 1.50214, 1.51063, 1.69562, 2.02342, 3.20611, 4.80401]*1e-5; # [cal/K/cm^2/sec]\n",
    " \n",
    "     # first value different from zero to prevent negative values at the pole during extrapolation\n",
    "     k_2 = [0.3, 0.9314, 1.9772, 3.4348, 4.8316, 3.7359, 0.6903, -2.5401, -10.5975]*1e-2; # [cal/dyn/sec]\n",
    "     \n",
    "     # southern hemisphere gets the same constants \n",
    "     T_0 = [T_0; reverse(T_0[1:end-1])]\n",
    "     C = [C; reverse(C[1:end-1])]\n",
    "     Q = [Q; reverse(Q[1:end-1])]\n",
    "     b = [b; reverse(b)]\n",
    "     z = [z; reverse(z)]\n",
    "     k_1 = [k_1; reverse(k_1)]\n",
    "     k_2 = [k_2; reverse(k_2)]\n",
    "     \n",
    "     #flat boundary conditions correspond due to the Neumann BC\n",
    "     T_0 = CubicSplineInterpolation(colat_1, T_0, bc=Interpolations.Flat(OnGrid()))\n",
    "     C = CubicSplineInterpolation(colat_1, C, bc=Interpolations.Flat(OnGrid()))\n",
    "     Q = CubicSplineInterpolation(colat_1, Q, bc=Interpolations.Flat(OnGrid()))\n",
    "     b = CubicSplineInterpolation(colat_2, b, bc=Interpolations.Flat(OnCell()))\n",
    "     z = CubicSplineInterpolation(colat_2, z, bc=Interpolations.Flat(OnCell()))\n",
    "     k_1 = CubicSplineInterpolation(colat_2, k_1, bc=Interpolations.Flat(OnCell()))\n",
    "     k_2 = CubicSplineInterpolation(colat_2, k_2, bc=Interpolations.Flat(OnCell()))\n",
    "\n",
    "     return T_0, C, Q, b, z, k_1, k_2\n",
    "end\n",
    "\n",
    "Base.@kwdef struct ContinousGhilSellersParameters{T}\n",
    "    T_0::Interpolations.Extrapolation{T}\n",
    "    μ::T\n",
    "    C::Interpolations.Extrapolation{T}\n",
    "    Q::Interpolations.Extrapolation{T}\n",
    "    b::Interpolations.Extrapolation{T}\n",
    "    z::Interpolations.Extrapolation{T}\n",
    "    k_1::Interpolations.Extrapolation{T}\n",
    "    k_2::Interpolations.Extrapolation{T}\n",
    "    c_1::T = 0.009\n",
    "    c_2::T = 0.0065\n",
    "    c_3::T = 1.9e-15\n",
    "    c_4::T = 6.105*0.75*exp(19.6)\n",
    "    c_5::T = 5350. \n",
    "    σ::T = 1.356e-12 # Stefan Boltzmann constant (in CGS system)\n",
    "    m::T = 0.5 # atmospheric attenuation coefficient (0.5 for present conditions)\n",
    "    T_m::T = 283.16;\n",
    "    α_max::T = 0.65;\n",
    "end \n",
    "\n",
    "function ContinousGhilSellersParameters(μ=1., m=0.5)\n",
    "    T_0, C, Q, b, z, k_1, k_2 = load_interpolated_parameters()  \n",
    "    ContinousGhilSellersParameters(T_0=T_0, μ=μ, C=C, Q=Q, b=b, z=z, k_1=k_1, k_2=k_2, m=m)\n",
    "end \n",
    "\n",
    "p = ContinousGhilSellersParameters()\n",
    "\n",
    "α(x, T, p::ContinousGhilSellersParameters) = cutoff_function.(p.b(x) - p.c_1 .* (p.T_m .+ min.(0, T - p.c_2 .* p.z(x) .- p.T_m)), p.α_max)\n",
    "\n",
    "function cutoff_function(x, max_val) \n",
    "    if x <= 0.25\n",
    "        return 0.25\n",
    "    elseif x >= max_val\n",
    "        return max_val\n",
    "    else \n",
    "        return x\n",
    "    end \n",
    "end\n",
    "\n",
    "\"\"\"\n",
    "    k(x,T,p::ContinousGhilSellersParameters)\n",
    "\n",
    "Heat flux, first term sensible heat flux, second term latent heat flux\n",
    "\"\"\"\n",
    "k(x, T, p::ContinousGhilSellersParameters) = p.k_1(x) + p.k_2(x) .* g(T,p)\n",
    "\n",
    "\"\"\"\n",
    "    g(T,p::ContinousGhilSellersParameters)\n",
    "\n",
    "Latent heat flux contribution\n",
    "\"\"\"\n",
    "g(T,p::ContinousGhilSellersParameters) = p.c_4 .* exp.(-p.c_5 ./ T) ./ (T.^2)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "b121f570",
   "metadata": {},
   "outputs": [],
   "source": [
    "D(x, T) = (2/π)^2 * cos(π*x/2)*k(x, T, p)\n",
    "S(x, T) = cos(π*x/2) * (p.μ*p.Q(x)*(1. - α(x, T, p)) - p.σ*T^4*(1.0 - p.m*tanh(p.c_3*T^6)))\n",
    "C(x) = p.C(x)*cos(π*x/2)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5add1a53",
   "metadata": {},
   "outputs": [],
   "source": [
    "n = 11\n",
    "knots = collect(range(-1, 1, length=n))\n",
    "SC = SplineComplex(knots, 4)\n",
    "\n",
    "tspan = (0, 3e9)\n",
    "f(x) = 273.\n",
    "u0 = initial_coefficients(SC, f)\n",
    "prob = NonlinearDiffusionProblem(SC, C, D, S, u0, tspan)\n",
    "\n",
    "sol = solve(prob, Tsit5());"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "73396284",
   "metadata": {},
   "outputs": [],
   "source": [
    "heatmap(SC, sol)"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "5978671e",
   "metadata": {},
   "outputs": [],
   "source": [
    "fig, ax, ln = diffusionplot(SC, sol, 100)\n",
    "lines!(ax, SC, sol[end], color=:red, linestyle=:dash, linewidth=3)\n",
    "fig"
   ]
  },
  {
   "cell_type": "code",
   "execution_count": null,
   "id": "ccf1cd64",
   "metadata": {},
   "outputs": [],
   "source": []
  }
 ],
 "metadata": {
  "kernelspec": {
   "display_name": "Julia 1.8.5",
   "language": "julia",
   "name": "julia-1.8"
  },
  "language_info": {
   "file_extension": ".jl",
   "mimetype": "application/julia",
   "name": "julia",
   "version": "1.8.5"
  }
 },
 "nbformat": 4,
 "nbformat_minor": 5
}
